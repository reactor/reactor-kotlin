// Need https://github.com/Kotlin/dokka/issues/184 to be fixed to avoid "Can't find node by signature" log spam
task dokka(type: org.jetbrains.dokka.gradle.DokkaTask) {
  dependsOn jar
  group = "documentation"
  description = "Generates Kotlin API documentation."
  moduleName = "reactor-kotlin"
  jdkVersion = 8

  outputFormat = "html"
  outputDirectory = new File(project.buildDir, "docs/kdoc")

  sourceDirs = files("src/main/kotlin")

  externalDocumentationLink {
    url = new URL("https://projectreactor.io/docs/core/release/api/")
  }
  externalDocumentationLink {
    url = new URL("https://www.reactive-streams.org/reactive-streams-1.0.2-javadoc/")
  }
}

task kdocZip(type: Zip, dependsOn: dokka) {
  //ends up similar to javadoc jar: reactor-core-xxxx.RELEASE-kdoc.zip
  classifier = 'kdoc'
  from(dokka.outputDirectory)
}

artifacts {
  archives kdocZip
}

dependencies {
  compile 'io.projectreactor:reactor-core:3.3.0.BUILD-SNAPSHOT'
  optional 'io.projectreactor:reactor-test:3.3.0.BUILD-SNAPSHOT'

  optional 'org.jetbrains.kotlin:kotlin-stdlib'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.12.2'
  testCompile 'io.projectreactor:reactor-test:3.3.0.BUILD-SNAPSHOT'
}